/*
 * TacOS Source Code
 *    Tokuyama kousen Advanced educational Computer.
 *
 * Copyright (C) 2011-2016 by
 *                      Dept. of Computer Science and Electronic Engineering,
 *                      Tokuyama College of Technology, JAPAN
 *
 *   上記著作権者は，Free Software Foundation によって公開されている GNU 一般公
 * 衆利用許諾契約書バージョン２に記述されている条件を満たす場合に限り，本ソース
 * コード(本ソースコードを改変したものを含む．以下同様)を使用・複製・改変・再配
 * 布することを無償で許諾する．
 *
 *   本ソースコードは＊全くの無保証＊で提供されるものである。上記著作権者および
 * 関連機関・個人は本ソースコードに関して，その適用可能性も含めて，いかなる保証
 * も行わない．また，本ソースコードの利用により直接的または間接的に生じたいかな
 * る損害に関しても，その責任を負わない．
 *
 *
 */

/*
 * shell/shell.cmm : シェルプログラム
 *
 * 2016.11.11 : 履歴機能の追加
 * 2016.01.13 : コマンドプログラムの生成方法を修正
 * 2015.12.07 : コマンドプログラムを bin フォルダから探すように変更
 * 2015.09.01 : コマンド実行後の sta の値のチェックを追加
 * 2015.07.16 : rm コマンドと cp コマンドを実装
 * 2015.07.01 : コマンド行引数の取得を追加
 * 2015.06.17 : 既存の kbdMain をコピー、ユーザプロセスとして動作するように改良
 *
 * $Id$
 *
 */

#include <syslib.h>
#include <lib.h>

#define LMAX 80                                         // 1行最大 80 文字

// s[ptr] からの空白区切り文字列を d に取り出す
int getStr(char[] s, int ptr, char[] d) {
  while (s[ptr]==' ')
    ptr = ptr + 1;

  int i=0;
  while (s[ptr]!='\0' && s[ptr]!=' ') {
    d[i] = s[ptr];
    i = i + 1;
    ptr = ptr + 1;
  }

  d[i] = '\0';
  return ptr;
}

// 文字列比較
int strCmp(char[] s1, char[] s2) {
   int i = 0;
   while (s1[i]==s2[i] && s1[i]!='\0')
     i = i + 1;
   return ord(s1[i])-ord(s2[i]);
}

// 文字列の長さを計算する('\0' はカウントしない)
public int strLen(char[] s) {
  int len = 0;
  while (s[len]!='\0')                              // 終端記号まで
    len = len + 1;
  return len;                                       // 長さを返す
}

// 文字列のコピー(s2 を s1 にコピーする)
void strCpy(char[] s1, char[] s2) {
  int i;
  for (i=0; s2[i]!='\0'; i=i+1)
    s1[i]=s2[i];
  s1[i]='\0';
}

char[] tbuf = array(LMAX+1);                            // 一時領域

// コマンド行引数の数を数える
int cntArgc(char[] s) {
  int argc = 0;                                         // 返り値
  int i=0;
  while (s[i]!='\0') {                                  // 終端記号が来るまで
    i = getStr(s, i, tbuf);                             //   コマンド行引数抽出
    argc = argc+1;                                      //   argcを加算
  }
  return argc;
}

char[] cc = array(2);

void putCh(char c){
  cc[0] = c;
  cc[1] = '\0';
  conWrite(cc);
}

#define BACKUPLEN 4
char[][] bufBackup = array(BACKUPLEN, LMAX+1);                         // キー入力文字列バックアップ用
int backupPoint = -1;

// readConsole (キーボード入力された文字列をユーザ領域に渡す)
int readConsole(char[] buf, int len) {
  int ch = 0;
  int kbdCnt = 0;                                       // buf のインデクス
  len = len - 1;                                        // '\0' の格納領域を確保
  for (;;) {
    while( (ch=getPS2())==0 )                             // 1文字入力
      ;
    if (ch==0x80) {
      if(backupPoint<BACKUPLEN-1 && strLen(bufBackup[backupPoint+1])>0){
        backupPoint = backupPoint+1;
        for(int i=0;i<kbdCnt;i=i+1){
          putCh('\x08');                                    //   カーソル位置を左に
          putCh(' ');                                       //   空白を出力
          putCh('\x08');                                    //   カーソル位置を左に
        }
        strCpy(buf, bufBackup[backupPoint]);
        kbdCnt = strLen(buf);
        for(int i=0;i<kbdCnt;i=i+1)
          putCh(buf[i]);
        }
    }else if(ch==0x81){
      if(backupPoint>0){
        backupPoint = backupPoint-1;
        for(int i=0;i<kbdCnt;i=i+1){
          putCh('\x08');                                    //   カーソル位置を左に
          putCh(' ');                                       //   空白を出力
          putCh('\x08');                                    //   カーソル位置を左に
        }
        strCpy(buf, bufBackup[backupPoint]);
        kbdCnt = strLen(buf);
        for(int i=0;i<kbdCnt;i=i+1)
          putCh(buf[i]);
        }else{
          for(int i=0;i<kbdCnt;i=i+1){
          putCh('\x08');                                    //   カーソル位置を左に
          putCh(' ');                                       //   空白を出力
          putCh('\x08');                                    //   カーソル位置を左に
        }
        buf[0] = '\0';
        kbdCnt = 0;
        }
    }else{
      char c = chr(ch);
      if (kbdCnt>=len || c=='\r' || c=='\n') {            // 指定長以上か改行なら
        buf[kbdCnt] = '\0';
        putCh('\n');
        if(strLen(buf)>0){
          backupPoint=-1;
          for(int i=BACKUPLEN-1; i>0; i=i-1)
            strCpy(bufBackup[i], bufBackup[i-1]);
          strCpy(bufBackup[0], buf);
        }
        break;
      } else if (0x20<=ord(c) && ord(c)<=0x7e && kbdCnt<len) { // 表示可能な文字
        buf[kbdCnt] = c;
        putCh(c);
        kbdCnt = kbdCnt + 1;
      } else if (c=='\x08'&& kbdCnt>0) {                  // バックスペースなら
        putCh('\x08');                                    //   カーソル位置を左に
        putCh(' ');                                       //   空白を出力
        putCh('\x08');                                    //   カーソル位置を左に
        kbdCnt = kbdCnt - 1;
      }
    }
  }
  return kbdCnt;
}

// 文字列の連結(src に dst を連結する)
void strCat(char[] src, char[] dst) {
  int i;
  for (i=0; src[i]!='\0'; i=i+1) ;                     // src の終端まで
  for (int j=0; (src[i]=dst[j])!='\0'; j=j+1)          // src の続きから
    i=i+1;                                             //   dst をコピーする
}

// メインルーチン
char[] buf = array(LMAX+1);                             // キー入力文字列格納用
char[][] argv;
char[] cmd = array(17);                                 // "bin/abcdefgh.exe"
int[]  sta = array(1);                                  // リターンコード格納用
public int main() {
  printf("TacOS Modoki Shell\n");
  buf[0]='\0';
  for(int i=0;i<BACKUPLEN;i=i+1)
    bufBackup[i][0]='\0';
  for (;;) {
    printf("$ ");                                       // プロンプトのつもり
    readConsole(buf, LMAX+1);                           // 入力文字列を取得

    // argc を計算
    int argc = cntArgc(buf);                            // コマンド行引数の数
    argv = malloc((argc+1) * 2);

    // argv を生成
    int p = 0;                                          // tbuf の参照位置
    int j = 0;                                          // argv のインデックス
    while (buf[p]!='\0') {
      p = getStr(buf, p, tbuf);                         // argv[i] を取得
      argv[j] = malloc(strLen(tbuf)+1);
      strCpy(argv[j], tbuf);
      j = j + 1;
    }
    argv[j] = null;

    // 入力に応じたプログラムを生成する
    if (argv[0][0]=='\n')                               // Enter だけなら
      ;                                                 //   何もしない
    else if(strLen(argv[0])>8)
      conWrite("Command name is too long.\n");
    else {
      strCpy(cmd, "bin/");                              // コマンドのパスを生成
      strCat(cmd, argv[0]);
      strCat(cmd, ".exe");
      if      (exec(cmd, argv)<0) perror(cmd);          // コマンドを生成
      else if (wait(sta)<0)       perror(cmd);          // コマンドの終了を待つ
    }

    if (sta[0]<0) _perror(argv[0], sta[0]);             // 異常終了のエラー表示
    for (int i=0; i<argc; i=i+1)                        // argv を解放
      free(argv[i]);
    free(argv);
  }
  return 0;
}
