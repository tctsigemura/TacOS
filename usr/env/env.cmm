/*
 * TacOS Source Code
 *    Tokuyama kousen Advanced educational Computer.
 *
 * Copyright (C) 2019 by
 *                      Dept. of Computer Science and Electronic Engineering,
 *                      Tokuyama College of Technology, JAPAN
 *
 *   上記著作権者は，Free Software Foundation によって公開されている GNU 一般公
 * 衆利用許諾契約書バージョン２に記述されている条件を満たす場合に限り，本ソース
 * コード(本ソースコードを改変したものを含む．以下同様)を使用・複製・改変・再配
 * 布することを無償で許諾する．
 *
 *   本ソースコードは＊全くの無保証＊で提供されるものである。上記著作権者および
 * 関連機関・個人は本ソースコードに関して，その適用可能性も含めて，いかなる保証
 * も行わない．また，本ソースコードの利用により直接的または間接的に生じたいかな
 * る損害に関しても，その責任を負わない．
 *
 *
 */

/*
 * env/env.cmm : 環境変数の一覧もしくは環境変数を設定したコマンドの実行
 *
 * 2019.11.14 : 新規作成
 *
 * $Id$
 *
 */

#include <stdio.hmm>
#include <stdlib.hmm>
#include <string.hmm>
#include <syslib.hmm>

#define PATHMAX 80
#define BIN "/bin/"
#define EXE ".exe"

int[] sta = array(1);

public int main(int argc, char[][] argv) {

  int index = 1;
  while (!putEnv(argv[index])) index = index + 1;   // 環境変数をすべて追加

  if (argv[index] == null) {                        // コマンドがなければ
    for (int i = 0; environ[i] != null; i = i+1)    //   すべての環境変数を
      printf("%s\n", environ[i]);                   //     出力して
    return 0;                                       //   終了
  }

  int cArgc = argc - index;
  char[][] cArgv = rMalloc(cArgc + 1);
  for (int i = 0; i < cArgc; i = i+1)               // 残りの引数を
    cArgv[i] = argv[index + i];                     //   cArgv にコピー
  cArgv[cArgc] = null;                              // null 終端

  char[] cmd = cMalloc(strLen(BIN)+strLen(cArgv[0])+strLen(EXE)+1);
  if (cmd == null) {                                // 確保できなかった
    perror(argv[0]);
    return 1;
  }

  strCpy(cmd, BIN);
  strCat(cmd, cArgv[0]);
  strCat(cmd, EXE);

  if (exec(cmd, cArgv, environ) < 0) perror(cmd);   // コマンドを実行

  if (wait(sta) < 0) perror(cmd);                   // 終了を待つ

  free(cmd);
  free(cArgv);

  return 0;
}
