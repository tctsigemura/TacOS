/*
 * TacOS Source Code
 *    Tokuyama kousen Advanced educational Computer.
 *
 * Copyright (C) 2011-2019 by
 *                      Dept. of Computer Science and Electronic Engineering,
 *                      Tokuyama College of Technology, JAPAN
 *
 *   上記著作権者は，Free Software Foundation によって公開されている GNU 一般公
 * 衆利用許諾契約書バージョン２に記述されている条件を満たす場合に限り，本ソース
 * コード(本ソースコードを改変したものを含む．以下同様)を使用・複製・改変・再配
 * 布することを無償で許諾する．
 *
 *   本ソースコードは＊全くの無保証＊で提供されるものである。上記著作権者および
 * 関連機関・個人は本ソースコードに関して，その適用可能性も含めて，いかなる保証
 * も行わない．また，本ソースコードの利用により直接的または間接的に生じたいかな
 * る損害に関しても，その責任を負わない．
 *
 *
 */

/*
 * tty/tty.cmm : 端末サーバの本体、キーボード入力の受付とディスプレイ表示をする
 *
 * 2019.05.10 : VGA, PS2を削除; 非サーバ化
 * 2018.11.30 : ps2 を独立させた
 * 2018.01.27 : インクルードファイルの拡張子を .h から .hmm に変更
 * 2016.01.03 : 中身を sysConRead() に移動し getBuf() を廃止(重村)
 * 2015.09.08 : strcpy() は util.cmm のものを使うように変更(重村)
 * 2015.09.03 : receive() が LINK を返すので getLink() の使用を中止(重村)
 * 2015.06.05 : ウインドウへの出力を排除
 * 2015.06.04 : 村田開発開始、TTY サーバ化、getLBuf を一部変更
 * 2013.01.08 : TaC-CPU V2 対応
 * 2011.05.20 : 新規作成
 *
 * $Id$
 *
 */

#include <util.hmm>
#include <kernel.hmm>
#include <syscall.hmm>

// --------------------------
#include <sio.hmm>
#define getCh() getSIO()
#define putCh(c) putSIO(c)
#define getChNonblock() getSIONonblock()

#include "tty.hmm"

int ttySem;

int ttyMode = TTY_MODE_COOKED;

int getChar() {
  if ((ttyMode&TTY_MODE_NONBLOCK)==0) {
    return getCh();
  }
  return getChNonblock();
}

// -------------------------- conRead システムコール --------------------------

// conRead システムコールの本体 (キーボード入力された文字列をユーザ領域に渡す)
int sysConRead(char[] buf, int len) {
  int kbdCnt = 0;                                       // buf のインデクス
  len = len - 1;                                        // '\0'の格納領域を確保
  if ((ttyMode&TTY_MODE_COOKED)!=0) {                   // COOKEDモードの場合
    for (;;) {
      int i = getChar();                                //   1文字入力
      if (i<0) {                                        //   読み込めなかった場合
        buf[kbdCnt] = '\0';
        break;
      }
      char c = chr(i);
      if (kbdCnt>=len || c=='\r' || c=='\n') {          //   指定長以上か改行なら
        buf[kbdCnt] = '\0';
        putCh('\n');
        break;
      }
      if (0x20<=ord(c) && ord(c)<=0x7e && kbdCnt<len) { // 表示可能な文字
        buf[kbdCnt] = c;
        putCh(c);
        kbdCnt = kbdCnt + 1;
      } else if (c=='\x08'&& kbdCnt>0) {                //   バックスペースなら
        putCh('\x08');                                  //     カーソル位置を左に
        putCh(' ');                                     //     空白を出力
        putCh('\x08');                                  //     カーソル位置を左に
        kbdCnt = kbdCnt - 1;
      }
    }
  } else {                                                // RAWモード
    for (;;) {
      int i = getChar();                                //   1文字入力
      if (i<0) {                                        //   読み込めなかった場合
        buf[kbdCnt] = '\0';
        break;
      }
      char c = chr(i);
      if(kbdCnt>=len) {
        buf[kbdCnt] = '\0';
        break;
      } else {
        buf[kbdCnt] = c;
        kbdCnt = kbdCnt + 1;
      }
    }
  }
  return kbdCnt;
}

// -------------------------- conWrite システムコール -------------------------

// conWrite システムコールの本体 (文字列をディスプレイに表示する)
//   buf はユーザ領域でフォーマット解読済みの文字列となっている
int sysConWrite(void[] buf) {
  char[] str=buf;                                       // 型変換
  for (int i=0; str[i]!='\0'; i=i+1) {
    putCh(str[i]);
  }
  return 0;
}

// -------------------------- conCtl システムコール ---------------------------

// conCtl システムコールの本体
int sysConCtl(int request, int argp) {
  if(request==CONCTL_GETMODE) {
    return ttyMode;
  } else if (request==CONCTL_SETMODE) {
    ttyMode = argp;
  } else if (request==CONCTL_SETBUF) {
    //TODO
  }
  return 0;
}

// セマフォで衝突回避
int ttySysCall(int op, int prm1, int prm2) {
  semP(ttySem);
  if (op==CONREAD) {
    op = sysConRead(_ItoA(prm1), prm2);                 // conRead(buf, len)
  } else if (op==CONWRITE) {
    op = sysConWrite(_ItoA(prm1));                      // conWrite(buf)
  } else if (op==CONCTL) {
    op = sysConCtl(prm1, prm2);                         // conCtl(request, argp)
  } else  op= -1;                                       // エラー
  semV(ttySem);
  return op;
}

//----------------------------------------------------------------------------
// システムコールインターフェイス
//   クライアントプロセスが以下のルーチンを呼出す
//----------------------------------------------------------------------------
// conRead システムコール
public int conRead(void[] buf, int len) {
  int r = ttySysCall(CONREAD, _AtoI(buf), len);
  return r;
}

// conWrite システムコール
public int conWrite(void[] buf) {
  int r = ttySysCall(CONWRITE, _AtoI(buf), 0);
  return r;
}

// conCtl システムコール
public int conCtl(int request, int argp) {
  int r = ttySysCall(CONCTL, request, argp);
  return r;
}

// 初期化
public void ttyInit() {
  ttySem = newSem(1);
}
