/*
 * TacOS Source Code
 *    Tokuyama kousen Advanced educational Computer.
 *
 * Copyright (C) 2016 by
 *                      Dept. of Computer Science and Electronic Engineering,
 *                      Tokuyama College of Technology, JAPAN
 *
 *   上記著作権者は，Free Software Foundation によって公開されている GNU 一般公
 * 衆利用許諾契約書バージョン２に記述されている条件を満たす場合に限り，本ソース
 * コード(本ソースコードを改変したものを含む．以下同様)を使用・複製・改変・再配
 * 布することを無償で許諾する．
 *
 *   本ソースコードは＊全くの無保証＊で提供されるものである。上記著作権者および
 * 関連機関・個人は本ソースコードに関して，その適用可能性も含めて，いかなる保証
 * も行わない．また，本ソースコードの利用により直接的または間接的に生じたいかな
 * る損害に関しても，その責任を負わない．
 *
 *
 */

 /*
  * ss/ss.cmm : ファイル送信プログラム
  *
  * 2019.01.28 : 新規作成
  *
  * $Id$
  *
  */


#include <stdio.hmm>
#include <stdlib.hmm>
#include <string.hmm>

#define BLOCK_SIZE 128

int[] filesize = array(2);
char[] buffer = array(128);

void output(int length, char[] buffer, boolean wait_for_ack);

public int main(int argc, char[][] argv){

  if (argc < 2){ /* ファイル名がない */
    fprintf(stderr, "Usage: %s <filename>\n", argv[0]);
    return 1;
  }
  /* ファイル長を取得 */
  if (fsize(argv[1], filesize)){
    perror(argv[1]);
    return 1;
  }

  FILE fp = fopen(argv[1], "r");
  if (fp == null){ /* ファイルがない */
    fprintf(stderr, "file not found\n");
    return 1;
  }

  printf("%s\n", argv[1]); /* ファイル名を出力 */
  printf("%d\n", filesize[1]); /* ファイル長の下位32bit */
  printf("%d\n", BLOCK_SIZE); /* ブロックサイズ */

  //int checksum = 0;

  int length = 0;
  while (!feof(fp)){
    char c = fgetc(fp);
    buffer[length] = c;
    length = length + 1;

    if (length == BLOCK_SIZE){
      output(length, buffer, true);
      length = 0;
    }
  }
  if (length > 0) {
    output(length, buffer, false);
  }

  return 0;
}

void output(int length, char[] buffer, boolean wait_for_ack){
  for (int i = 0; i < length; i = i + 1){
    fprintf(stdout, "%d\n", ord(buffer[i]));
  }
  fflush(stdout);

  /* Ackを待つ */
  if (wait_for_ack){
    fgets(buffer, BLOCK_SIZE, stdin);
    if (strNcmp(buffer, "Ack\n", 4) != 0){
      fprintf(stderr, "error\n");
      exit(-1); /* Ackではないものが返ってきたので異常終了 */
    }
  }
}
